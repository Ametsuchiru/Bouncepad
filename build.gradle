plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.7'
}

group 'com.cleanroommc'
version '0.10'
archivesBaseName = 'bouncepad'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

def compilerArgs = [
        '--add-exports=java.base/jdk.internal.access=ALL-UNNAMED',
        '--add-exports=java.base/jdk.internal.loader=ALL-UNNAMED',
]

repositories {
    mavenCentral()
    maven {
        name 'Minecraft'
        url 'https://libraries.minecraft.net/'
    }
    maven {
        name 'CleanroomMC'
        url 'https://maven.cleanroommc.com'
    }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    implementation 'zone.rong:imaginebreaker:2.1'

    implementation 'org.apache.logging.log4j:log4j-api:2.22.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.22.0'
    annotationProcessor 'org.apache.logging.log4j:log4j-core:2.22.0'

    implementation 'org.jline:jline:3.25.1'
    implementation 'org.jline:jline-terminal-jansi:3.25.1'
    implementation ('net.minecrell:terminalconsoleappender:1.3.0') {
        transitive = false
    }

    implementation 'org.ow2.asm:asm:9.6'
    implementation 'org.ow2.asm:asm-commons:9.6'
    implementation 'org.ow2.asm:asm-tree:9.6'

    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.openjdk.jmh:jmh-core:1.36'
    testImplementation 'org.openjdk.jol:jol-core:0.17'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.36'
}

processResources {
    from (configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/**'
    }
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding 'UTF-8'
    options.compilerArgs.addAll(compilerArgs)
}

tasks.register('bounce', JavaExec) {
    doFirst {
        println javaVersion
    }
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs '-Djava.system.class.loader=com.cleanroommc.bouncepad.BouncepadClassLoader'
    systemProperty 'bouncepad.doNotProcessArguments', 'true'
    mainClass = 'com.cleanroommc.bouncepad.Bouncepad'
}

idea.project.settings.compiler.javac.javacAdditionalOptions = compilerArgs.join(' ')

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'bouncepad'
        }
    }
    repositories {
        maven {
            name = 'CleanroomMaven'
            url = CleanroomMaven
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}